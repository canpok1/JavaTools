apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'findbugs'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'com.googlecode.jmockit:jmockit:1.5'
    testCompile 'com.googlecode.jmockit:jmockit-coverage:0.999.24'
    testCompile 'junit:junit:4.11'
}

// 文字コード設定
def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava, javadoc]*.options*.encoding = defaultEncoding
// Gradleラッパーのインストールタスク
task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

// jarのマニフェスト設定
jar {
    manifest {
        attributes "Main-Class" : "jp.gr.java_conf.ktnet.bom.BomEraser"
    }
}

// 成果物をZip圧縮
task zip(type: Zip) {
    // batフォルダ => zipのルート
    from 'bat'

    // アプリケーションのJAR => zipのlibフォルダ
    into('lib') {
        from "build/libs/BomEraser.jar"
    }

    // Zipの出力先
    destinationDir = file(".")
}

test {
    // テストが二重実行される問題の対策
    exclude '**/*$*'
}

findbugsMain {
    effort = "max"
    reportLevel = "high"
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = false
    }
}
findbugsTest {
    effort = "max"
    reportLevel = "high"
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

defaultTasks 'clean', 'build', 'zip', 'javadoc'
